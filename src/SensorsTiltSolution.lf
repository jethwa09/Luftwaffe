/**
 * Display three dimensions of accelerometer measurements on the LCD display of the
 * <a href="https://www.pololu.com/docs/0J86">Pololu 3pi+ 2040 robot</a>. To run this program, first
 * put the robot in BOOTSEL mode (hold button B while resetting). Then the sequence of commands is
 * something like this:
 * ```
 *     $ cd ~/lf-pico
 *     $ lfc src/AccelerometerDisplay.lf
 *     ...
 *     $ picotool load -x bin/AccelerometerDisplay.elf
 * ```

 *
 * This compiles the program, loads it into flash memory on the robot, and begins executing it.
 *
 * @author Edward A. Lee
 */
 target C {
    platform: "RP2040",
    single-threaded: true
  }
  
  import Accelerometer from "lib/IMU.lf"
  import Display from "lib/Display.lf"
  import Tilt from "lib/Tilt.lf"
  
  main reactor {
    a = new Accelerometer()
    d = new Display()
    tilt = new Tilt()
    timer t(0, 250 msec)

    a.x -> tilt.ax
    a.y -> tilt.ay
    a.z -> tilt.az
  
    reaction(t) -> a.trigger {=
      lf_set(a.trigger, true);
    =}
  
    reaction(tilt.pitch, tilt.roll, tilt.yaw) -> d.line0, d.line1, d.line2 {=
      /// TODO: define max string size for line
      /// based on font you can have 4 or 8 lines
      static char buf0[20];
      static char buf1[20];
      static char buf2[20];
  
      snprintf(buf0, 20, "pitch:%2.4f", tilt.pitch->value);
      snprintf(buf1, 20, "roll:%2.4f", tilt.roll->value);
      snprintf(buf2, 20, "yaw:%2.4f", tilt.yaw->value);
  
      lf_set(d.line0, buf0);
      lf_set(d.line1, buf1);
      lf_set(d.line2, buf2);
    =}
  }
  